<!DOCTYPE html>
<html>
<head>	
	<title>Surface with WebGL</title>	
	<meta charset="utf-8">
	<link rel="stylesheet" type="text/css" href="../common/css/style.css">	
	<script type="text/javascript" src="../lib/jquery-3.0.0.min.js"></script>		
	<script type="text/javascript" src="../lib/requestAnimationFrame.js"></script>		
	<script type="text/javascript" src="../lib/gl-matrix-min.js"></script>		
	<script type="text/javascript" src="../lib/wgl/wgl.js"></script>		
	<script type="text/javascript" src="../lib/wgl/mesh.js"></script>
	<script type="text/javascript" src="surface.js"></script>
</head>
<body>	
	<canvas id="canvas"></canvas>
	<div id="controls-pane">
		<table>
		<tr>
		<td><label>Mesh size:</label></td>
		<td><input type="number" id="mesh-size" value="50" min="1" max="100"></td>
		</tr>
		<tr>
		<td><label>Mesh points:</label></td>
		<td><input type="number" id="mesh-points" value="101" min="2" max="256"></td>
		</tr>		
		</table>
	</div>	

	<script type="text/javascript">	
		$(document).ready( function () {					
			var $meshSize = $("#mesh-size");
			var $meshPoints = $("#mesh-points");			
		
			init();			

			setMeshSize($meshSize.val());
			setMeshPoints($meshPoints.val());			

			render();

			$meshSize.change( function (event) {
				setMeshSize($(this).val());
			});
			$meshPoints.change( function (event) {
				setMeshPoints($(this).val());
			});			
		});
	</script>

	<script id="funcs" type="text/shader">
		precision mediump float;

		float surfaceFunc(float x, float y, float shift) {
			return 2.0*sin(sqrt(x*x + y*y) - shift);
		}

		float surfaceDfDx(float x, float y, float shift) {
			float sq = sqrt(x*x + y*y);
			return x*cos(sq - shift)/max(sq, 1e-6);
		}	

		float surfaceDfDy(float x, float y, float shift) {
			float sq = sqrt(x*x + y*y);
			return y*cos(sq - shift)/max(sq, 1e-6);
		}
	</script>	

	<script id="vert-shader" type="text/vert-shader">
		precision mediump float;

		attribute vec3 a_position; 				
		
		uniform float u_shift;
		uniform mat4 u_modelMatrix;		
		uniform mat4 u_viewMatrix;
		uniform mat4 u_projectionMatrix;				
		
		varying vec4 v_world_position;
		varying vec4 v_eye_position;
		
		void main() {					
			vec4 pos = vec4(a_position, 1.0);
  			vec4 world_pos = u_modelMatrix * pos;  	
  			world_pos.y += surfaceFunc(world_pos.x, world_pos.z, u_shift);
  			v_world_position = world_pos;		  			
  			v_eye_position = u_viewMatrix * v_world_position;
			gl_Position = u_projectionMatrix * v_eye_position;  		
		}
	</script>

	<script id="frag-shader" type="text/frag-shader">
		precision mediump float;
				
		varying vec4 v_world_position;
		varying vec4 v_eye_position;

		uniform float u_shift;
		uniform vec4 u_light_pos; // light pos in view space
		uniform mat3 u_normalViewMatrix;	

		vec3 ambient = vec3(0.1, 0.1, 0.1);
		vec3 light_diffuse = vec3(1.0, 1.0, 1.0);
		vec3 light_specular = vec3(1.0, 1.0, 1.0);				

		vec3 material_duffuse = vec3(0.0, 0.9, 0.9);
		vec3 material_specular = vec3(0.9, 1.0, 1.0);
		float material_shininess = 5.0;			

		void main() {				
			vec4 pos = v_eye_position; // pos in view space			
			vec4 light_dir = normalize(u_light_pos - pos); // direction towards the light  			
			
			float x = v_world_position.x;
			float z = v_world_position.z;
			float dfdx = surfaceDfDx(x, z, u_shift);
			float dfdy = surfaceDfDy(x, z, u_shift);
			vec3 world_norm = vec3(-dfdx, 1.0, -dfdy);

			vec4 norm = normalize(vec4(u_normalViewMatrix * world_norm, 0.0)); // normal in eye space
			vec4 refl = reflect(-light_dir, norm); // light reflection
			vec4 view = normalize(vec4(-pos.xyz, 0.0)); // view vector from the point to the eye (zero)

			float diffuse = max(0.0, dot(norm, light_dir));

			float specular = 0.0;
			if (dot(norm, view) > 0.0) {
				specular = pow(max(0.0, dot(refl, view)), material_shininess);
			} 

			vec3 color = clamp(ambient 
				+ diffuse*light_diffuse*material_duffuse 
				+ specular*light_specular*material_specular, 0.0, 1.0);			
	 		gl_FragColor = vec4(color, 1.0);  	 		
		}
	</script>
</body>
</html>